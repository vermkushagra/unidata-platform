ext.springVersion = '4.1.6.RELEASE'

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'org.flywaydb:flyway-gradle-plugin:3.2.1', 'org.postgresql:postgresql:9.4-1200-jdbc41'
    }
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'org.flywaydb.flyway'
apply plugin: 'application'

mainClassName = 'com.unidata.mdm.backend.db.update.FlywayUpdater'

flyway {
    url = rootProject.databaseUrl
    user = rootProject.databaseUser
    password = rootProject.databasePassword
    schemas = ['public']
    locations = ['classpath:db.migration']
}

compileJava {
    options.encoding = 'UTF-8'
    options.debug = true
}
compileTestJava {
    options.encoding = 'UTF-8'
    options.debug = true
}

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

idea {
    module {
        sourceDirs += file('src/main/generated')
        downloadJavadoc = true
        downloadSources = true
    }
}

dependencies {
    // DB engine and Drivers
    compile ('org.postgresql:postgresql:9.4-1200-jdbc41') { // BSD like
        exclude group: 'org.slf4j', module: 'slf4j-simple'
    }

    compile 'org.flywaydb:flyway-core:3.2.1'

    // Springframework - Apache License, v.2.0
    compile 'org.springframework:spring-beans:' + springVersion,
            'org.springframework:spring-jdbc:' + springVersion,
            'org.springframework:spring-context:' + springVersion,
            'org.springframework:spring-context-support:' + springVersion,
            'org.springframework:spring-webmvc:' + springVersion,
            'org.springframework:spring-web:' + springVersion,
            'org.springframework:spring-test:' + springVersion,
            'org.springframework:spring-tx:' + springVersion,
            'org.springframework:spring-aspects:' + springVersion,
            'org.springframework:spring-aop:' + springVersion,
            'org.springframework.data:spring-data-commons:1.9.2.RELEASE'

    compile ('com.beust:jcommander:1.48') {
        exclude group: 'org.slf4j', module: 'slf4j-simple'
    }

}

jar{
    manifest {
        attributes 'Main-Class': mainClassName
    }
}

task fatJar(type: Jar, group: 'build'){
    // Bundle all dependencies aka 'fat jar' to allow run from single file anywhere!
    from files(sourceSets.main.output.classesDir)
    from configurations.runtime.asFileTree.files.collect { zipTree(it) }

    archiveName = "${baseName}-${project.version}.svn.${getScmRevision()}.full.jar"
}

tasks.withType(Jar){ // Common for Jars
    baseName = 'unidata-db-updater'
    destinationDir = new File(project.rootDir, '/target')
}