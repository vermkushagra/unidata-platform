buildscript {
    ext {
        xjcVersion= '3.2.3'
        jaxbOutputDir = "$projectDir/src/main/generated"
        cxfVersion= '3.3.3'
    }
}

plugins {
    id 'java'
    id 'io.spring.dependency-management' version '1.0.8.RELEASE'
    id 'com.intershop.gradle.jaxb' version '3.0.4'
    id 'idea'
}

apply from: '../gradle/dependencies.gradle'

version 'unspecified'

sourceCompatibility = 1.8

idea {
    module {
        sourceDirs += file('src/main/generated')
        downloadJavadoc = true
        downloadSources = true
    }
}

compileJava {
    options.encoding = 'UTF-8'
    options.debug = true
    dependsOn 'wsdl2javaV1Login'
    source jaxbOutputDir, sourceSets.main.java
}

//repositories {
//    mavenCentral()
//    maven {
//        name = 'server1.local'
//        credentials  {
//            username = "$taskdataArtifactoryUser"
//            password = "$taskdataArtifactoryPassword"
//        }
//        url 'http://maven.taskdata.work:8081/artifactory/libs-snapshot-local/'
//    }
//
//}

configurations {
    cxf
}

dependencies {
    implementation project(':org.unidata.mdm.system')
    implementation project(':org.unidata.mdm.core')

    implementation 'org.springframework:spring-context'
    implementation 'org.springframework:spring-jdbc'
    implementation 'org.springframework:spring-tx'
    implementation 'org.springframework.security:spring-security-core'

    implementation 'io.vavr:vavr:0.10.0'
    
    implementation 'com.google.guava:guava:27.0.1-jre'

    implementation 'org.apache.commons:commons-lang3:3.9'
    implementation 'org.apache.commons:commons-collections4:4.3'

    implementation 'org.slf4j:slf4j-api:1.7.26'

    implementation 'com.google.code.findbugs:jsr305:3.0.2'

//    implementation 'nl.myndocs:database-migrator:1.2-SNAPSHOT'
    compile files('../extlib/database-migrator-1.2-SNAPSHOT.jar') //20190821.164456-1

    compile 'jakarta.ws.rs:jakarta.ws.rs-api:2.1.6'
    compile 'javax:javaee-api:7.0'

    compileOnly 'javax.servlet:javax.servlet-api:4.0.1'

    cxf "org.jvnet.jaxb2_commons:jaxb2-basics-annotate:1.1.0"
    cxf "net.java.dev.jaxb2-commons:jaxb-fluent-api:2.1.8"

    cxf "org.apache.cxf:cxf-tools-wsdlto-core:$cxfVersion"
    cxf "org.apache.cxf:cxf-tools-wsdlto-frontend-jaxws:$cxfVersion"
    cxf "org.apache.cxf:cxf-tools-wsdlto-databinding-jaxb:$cxfVersion"
    cxf "org.apache.cxf.xjcplugins:cxf-xjc-ts:$xjcVersion"
    cxf 'javax.xml.bind:jaxb-api:2.3.1'
    cxf 'javax.xml.ws:jaxws-api:2.3.1'
    cxf 'javax.jws:javax.jws-api:1.1'

    compile "org.apache.cxf.xjc-utils:cxf-xjc-runtime:$xjcVersion"
    compile "org.apache.cxf:cxf-rt-bindings-soap:$cxfVersion"
    compile "org.apache.cxf:cxf-rt-frontend-jaxws:$cxfVersion"

    testCompile 'org.junit.jupiter:junit-jupiter-api'
    testRuntime 'org.junit.jupiter:junit-jupiter-engine'
}

jar {
    manifest {
        attributes 'Unidata-Module-Class': 'org.unidata.mdm.soap.core.module.CoreSoapModule'
    }
}

task wsdl2javaV1Login(type: JavaExec) {
    ext {
        outputDir = file("$jaxbOutputDir")
    }
    systemProperties  = ['javax.xml.accessExternalSchema': 'file', 'file.encoding': 'UTF8']
    outputs.upToDateWhen { false }
    outputs.cacheIf { true }
    outputs.dir outputDir
    main = 'org.apache.cxf.tools.wsdlto.WSDLToJava'
    classpath = project.configurations.cxf
    args '-d', outputDir
    args '-fe', 'jaxws21'
    args '-all'
    args '-exsh', true
    args '-verbose'
    args '-validate'
    args '-mark-generated'
    args '-wsdlLocation', 'classpath:wsdl/login-api.wsdl'
    args '-xjc-Xfluent-api'
    args '-xjc-Xts'
    args '-xjc-Xannotate'
    args "$projectDir/src/main/resources/wsdl/login-api.wsdl"
    doLast {
        println "----- wsdl2javaV1Login - cxf jaxb2 files generated -----"
    }
}
